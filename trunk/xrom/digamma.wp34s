/* This file is part of 34S.
 * 
 * 34S is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * 34S is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with 34S.  If not, see <http://www.gnu.org/licenses/>.
 */

#ifdef INCLUDE_XROM_DIGAMMA
/**************************************************************************/
/* Real digamma function.
 *
 * This code is based on Jean-Marc Baillard's HP-41 version from:
 *	http://hp41programs.yolasite.com/gamma.php
 *
 * This uses a fourth order approximation and isn't sufficiently accurate for
 * inclusion as a built in function.
 *
 * The series used here and for the complex version is:
 *
 * Psi(x) = ln x - 1/(2^x) -1/(12x2) + 1/(120x^4) - 1/(252x&6) + 1/(240x^8)  is used for  x > 8 
 * Psi(x+1) = Psi(x) + 1/x 
 * Psi(-x) = 1/x + Psi(x) + Pi/Tan ( Pi x )
 *
 *
 * Adding more terms to the first of these should greatly improve accuracy:
 *
 * psi(x) = -log(1/x) - 1/(2 x) - 1/(12 x^2) + 1/(120 x^4) - 1/(252 x^6) +
 *		1/(240 x^8) - 1/(132 x^10) + 691/(32760 x^12) - 1/(12 x^14) +
 *		3617/(8160 x^16) - 43867/(14364 x^18) + 174611/(6600 x^20) -
 *		77683/(276 x^22) + 236364091/(65520 x^24) -
 *		657931/(12 x^26) + 3392780147/(3480 x^28) -
 *		1723168255201/(85932 x^30) + 7709321041217/(16320 x^32) -
 *		151628697551/(12 x^34) + 26315271553053477373/(69090840 x^36) -
 *		154210205991661/(12 x^38) + 261082718496449122051/(541200 x^40) +
 *	    O((1/x)^42)
 */
		XLBL"DIGAMMA"
			xIN MONADIC
			LocR 3
			SSIZE4
			x<0?
				SF .00
			ABS
			STO .02
			_INT 8
			SWAP
digamma_lp::		1/x
			STO+ .00
			x[<->] L
			INC X
			x<? Y
				JMP digamma_lp
			1/x
			STO .01
			x[^2]
			STO Y
			RCL Y
			_INT 20
			/
			_INT 21
			1/x
			-
			[times]
			Num 1
			SDR 1
			+
			[times]
			DEC X
			[times]
			_INT 12
			/
			RCL .01
			LN
			RCL L
			_INT 2
			/
			+
			-
			RCL- .00
			FC? .00
				xOUT xOUT_NORMAL
			RCL .02
			1/x
			+
			Num [pi]
			RCL .02
			RCL .02
			ROUNDI
			-
			Num [pi]
			[times]
			TAN
			/
			+
			xOUT xOUT_NORMAL

/**************************************************************************/
/* Complex digamma function.
 *
 * This uses a fourth order approximation and isn't sufficiently accurate for
 * inclusion as a built in function.
 */
		XLBL"CPX_DIGAMMA"
			xIN MONADIC_COMPLEX
			LocR 6
			x<0?
				SF .00
			FS? .00
				[cmplx]+/-
			[cmplx]STO .04
			[cmplx]_INT 8
			CSWAP
c_digamma_lp::		[cmplx]1/x
			[cmplx]STO+ .00
			[cmplx]x[<->] L
			INC X
			x<? Z
				JMP c_digamma_lp
			[cmplx]1/x
			[cmplx]STO .02
			[cmplx]RCL[times] X
			[cmplx]STO Z
			[cmplx]RCL Z
			[cmplx]_INT 20
			[cmplx]/
			[cmplx]_INT 21
			[cmplx]1/x
			[cmplx]-
			[cmplx][times]
			[cmplx]Num 1
			SDR 1
			[cmplx]+
			[cmplx][times]
			DEC X
			[cmplx][times]
			[cmplx]_INT 12
			[cmplx]/
			[cmplx]RCL .02
			[cmplx]LN
			[cmplx]RCL L
			[cmplx]_INT 2
			[cmplx]/
			[cmplx]+
			[cmplx]-
			[cmplx]RCL- .00
			FC? .00
				xOUT xOUT_NORMAL
			[cmplx]RCL .04
			[cmplx]1/x
			[cmplx]+
			[cmplx]Num [pi]
			[cmplx]RCL .04
			[cmplx]RCL[times] Z
			[cmplx]TAN
			[cmplx]/
			[cmplx]+
			xOUT xOUT_NORMAL

#endif

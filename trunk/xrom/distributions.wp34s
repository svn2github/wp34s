		XLBL"PDF_CAUCHY"
			xIN MONADIC
			SPEC?
				JMP cauchy_pdf_special
			GSB cauchy_xform
			RCL[times] X
			INC X
			RCL[times] K
			[pi]
			[times]
			1/x
			xOUT 00
cauchy_pdf_special::	0
			xOUT 00

		XLBL"CDF_CAUCHY"
			xIN MONADIC
			SPEC?
				JMP cauchy_cdf_special
			GSB cauchy_xform
			ATAN
			[pi]
			/
			.
			5
			+
			xOUT 00
			RTN

cauchy_xform::		RCL- J
			RCL K
			x[<=]0?
				ERR ERR_BAD_PARAM
			/
			RTN


cauchy_cdf_special::	x<0?
				JMP dist_NaN
			1
			xOUT 00

		XLBL"QF_CAUCHY"
			xIN MONADIC
			GSB check_probability
			.
			5
			-
			[pi]
			[times]
			TAN
			RCL K
			x[<=]0?
				ERR ERR_BAD_PARAM
			[times]
			RCL+ J

cauchy_exit::		x[<->] .08
			STO L
			RCLS .08
			RTN


/* Utility conditional that checks if the argument is a valid probability or not */

check_probability::	x<0?
				JMP dist_neginf
			x>1?
				JMP dist_posinf
			SPEC?
				JMP dist_NaN
			RTN

dist_neginf::		FC? D
				ERR ERR_MINFINITY
			Num -[infinity]
			xOUT 00

dist_posinf::		FC? D
				ERR ERR_INFINITY
			Num [infinity]
			xOUT 00

dist_NaN::		FC? D
				ERR ERR_DOMAIN
			Num NaN
			xOUT 00

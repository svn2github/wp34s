/**************************************************************************/
/* The positive branch of the real W function.
 * A rough guess is made and then Newton's method is used to attain
 * convergence.  The C version works similarily but uses Halley's
 * method which converges a cubically instead of quadratically.
 *
 * This code is based on Jean-Marc Baillard's HP-41 version from:
 *	http://hp41programs.yolasite.com/lambertw.php
 *
 * Uses regiser I for temproary storage.
 */

			XLBL"W0"
xrom_w0::		xIN MONADIC
			SPEC?
				JMP ret_NaN
			x=0?
				xOUT xOUT_NORMAL
			Num eE
			1/x
			+/-
			x[<->] Y
			x<? Y
				JMP ret_NaN
			STO I
			LN1+x
LamW0_loop::		FILL
			+/-
			e[^x]
			RCL[times] I
			-
			x[<->] Y
			INC X
			/
			-
			CNVG? CVG_RELATIVE
				xOUT xOUT_NORMAL
			JMP LamW0_loop

/**************************************************************************/
/* The negative branch of the real W function.
 * Uses registesr J and K for temporary storage.
 */

			XLBL"W1"
xrom_w1::		xIN MONADIC
			SPEC?
				JMP ret_NaN
			x>0?
				JMP ret_NaN
			x=0?
				JMP ret_neginf
			+/-
			1/x
			STO I
			Num eE
			x>? Y
				JMP ret_NaN
			RCL I
LamW1_loop::		STO J
			LN
			RCL[times] I
			RCL J
			x[<->] Y
			CNVG? CVG_RELATIVE
				JMP LamW_converged
			JMP LamW1_loop
LamW_converged::	RCL/ I
			+/-
			xOUT xOUT_NORMAL


/**************************************************************************/
/* The positive branch of the complex W function.
 *
 * This code is based on Jean-Marc Baillard's HP-41 version from:
 *	http://hp41programs.yolasite.com/lambertw.php
 *
 * Register use:
 *	.00/.01	z
 *	.02/.03	w
 *	.04/.05	temporary
 */
			XLBL"CPX_W0"
xrom_c_w::		xIN MONADIC_COMPLEX
			LocR 05
			[cmplx]STO .00
			iC 1
			STO .02
			STO .03
			+
			[cmplx]x=0?
				JMP LamW_cloop
			[cmplx]LN
			[cmplx]STO .02
LamW_cloop::		[cmplx]ENTER
			[cmplx]e[^x]
			[cmplx]STO .04
			[cmplx]RCL[times] .02
			[cmplx]STO+ .04
			[cmplx]RCL- .00
			[cmplx]RCL/ .04
			[cmplx]STO- .02
			[cmplx]DROP
			[cmplx]RCL .02
			CNVG? CVG_COMPLEX
				xOUT xOUT_NORMAL
			JMP LamW_cloop

/**************************************************************************/
/* The inverse W function in both real and complex domains.
 * These can be done inline without the usual prelude and epilogue code.
 */

			XLBL"W_INVERSE"
xrom_winv::		xIN MONADIC
				e[^x]
				RCL[times] L
			xOUT xOUT_NORMAL

			XLBL"CPX_W_INVERSE"
xrom_c_winv::		xIN MONADIC_COMPLEX
				[cmplx]e[^x]
				[cmplx]RCL[times] L
			xOUT xOUT_NORMAL

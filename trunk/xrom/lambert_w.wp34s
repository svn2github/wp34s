
/**************************************************************************/
/* The positive branch of the real W function.
 * A rough guess is made and then Newton's method is used to attain
 * convergence.  The C version works similarily but uses Halley's
 * method which converges a cubically instead of quadratically.
 *
 * This code is based on Jean-Marc Baillard's HP-41 version from:
 *	http://hp41programs.yolasite.com/lambertw.php
 */

		XLBL"W0"
xrom_wo::		xIN MONADIC
			SPEC?
				JMP LamW_NaN
			x=0?
				xOUT xOUT_NORMAL
			Num eE
			1/x
			+/-
			x[<->] Y
			x<? Y
				JMP LamW_NaN
			STO .00
			LN1+x
LamW0_loop::		FILL
			+/-
			e[^x]
			RCL[times] .00
			-
			x[<->] Y
			INC X
			/
			-
			CNVG? CVG_RELATIVE
				xOUT xOUT_NORMAL
			JMP LamW0_loop

/**************************************************************************/
/* The negative branch of the real W function.
 */

		XLBL"W1"
xrom_w1::		xIN MONADIC
			SPEC?
				JMP LamW_NaN
			x[>=]0?
				JMP LamW_inf
			+/-
			1/x
			STO .00
			Num eE
			x>? Y
				JMP LamW_NaN
			RCL .00
LamW1_loop::		STO .01
			LN
			RCL[times] .00
			RCL .01
			x[<->] Y
			CNVG? CVG_RELATIVE
				JMP LamW_converged
			JMP LamW1_loop
LamW_converged::	RCL/ .00
			+/-
			xOUT xOUT_NORMAL

LamW_inf::	x>0?
			JMP LamW_NaN
		FC? D
			ERR ERR_MINFINITY
		Num -[infinity]
		xOUT xOUT_NORMAL

LamW_NaN::	FC? D
			ERR ERR_DOMAIN
		Num NaN
		xOUT xOUT_NORMAL



/**************************************************************************/
/* The positive branch of the complex W function.
 * A rough guess is made and then Newton's method is used to attain
 * convergence.  The C version works similarily but uses Halley's
 * method which converges a cubically instead of quadratically.
 *
 * MORE - needs to be written.
 */
		XLBL"CPX_W0"

/**************************************************************************/
/* The inverse W function in both real and complex domains.
 * These can be done inline without the usual prelude and epilogue code.
 */

		XLBL"W_INVERSE"
xrom_winv::		xIN MONADIC
				e[^x]
				RCL[times] L
			xOUT xOUT_NORMAL

		XLBL"CPX_W_INVERSE"
xrom_c_winv::		xIN MONADIC_COMPLEX
				[cmplx]e[^x]
				[cmplx]RCL[times] L
			xOUT xOUT_NORMAL

/* This file is part of 34S.
 * 
 * 34S is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * 34S is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with 34S.  If not, see <http://www.gnu.org/licenses/>.
 */

/**************************************************************************/
/* Percentage calculations
 */

/* Percent
 * % = x . y / 100
 * Y is not consomued by this function.
 */
	XLBL"PERCENT"
		xIN MONADIC
			RCL[times] Y
			SDR 02
		xOUT xOUT_NORMAL

/* Percent change
 * %chg = 100 ( x - y ) / y
 * Y is not consomued by this function.
 */
 	XLBL"PERCHG"
		xIN MONADIC
			RCL- Y
			RCL/ Y
per_mul100::		SDL 02
		xOUT xOUT_NORMAL

/* Percent total
 * %tot = 100 . x / y
 * Y is not consomued by this function.
 */
	XLBL"PERTOT"
		xIN MONADIC
			RCL Y
			/
			JMP per_mul100

/* Margin
 * Margin = 100 (x - y) / x
 */
	XLBL"MARGIN"
		xIN DYADIC
			RCL- Y
			RCL/ L
			JMP per_mul100

/* Percent MMR
 * PerMRR = ((x/y) ^ 1/z - 1 ) * 100
 */
	XLBL"PERMMR"
		xIN TRIADIC
			x[<->] Y
			/
			x[<->] Y
			[^x][sqrt]y
			DEC X
			JMP per_mul100

/* Percent markup margin
 * Markup Margin = y / ( 1 - x / 100 )
 */
	XLBL"PERMARGIN"
		xIN DYADIC
			SDR 02
			+/-
			INC X
			/
		xOUT xOUT_NORMAL

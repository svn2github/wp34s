/* This file is part of 34S.
 * 
 * 34S is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * 34S is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with 34S.  If not, see <http://www.gnu.org/licenses/>.
 */

/**************************************************************************/
/* Weibull distribution
 * Two parameters:
 *	J = lambda (shape) > 0
 *	K = k (scale) > 0
 * Formulas:
 *	pdf = k/J (x/J)^(k-1) exp(-(x/J)^k)  x>=0, 0 otherwise
 *	cdf = 1 - exp( -(x/J)^k)
 *	qf = (-ln(1-p) ^ (1/k)) * J
 */
		XLBL"PDF_WEIB"
			xIN MONADIC
			GSB weibull_param
			RCL Y
			SPEC?
				JMP ret_0
			x[<=]0?
				JMP ret_0
			SWAP
			y[^x]
			STO Z
			+/-
			e[^x]
			RCL[times] Z
			RCL/ Y
			RCL[times] K
			RCL/ J
			xOUT xOUT_NORMAL

		XLBL"CDFU_WEIB"
			xIN MONADIC
			GSB weibull_param
			SWAP
			x[<=]0?
				JMP ret_1
			SPEC?
				JMP ret_0
			SWAP
			y[^x]
			+/-
			e[^x]
			xOUT xOUT_NORMAL

		XLBL"CDF_WEIB"
			xIN MONADIC
			GSB weibull_param
			SWAP
			x[<=]0?
				JMP ret_0
			SPEC?
				JMP ret_1	/* only +infinte left */
			SWAP
			y[^x]
			+/-
			e[^x]-1
			+/-
			xOUT xOUT_NORMAL

weibull_param::		RCL J
			GSB check_special_pos
			/
			RCL K
			GSB check_special_pos
			RTN

		XLBL"QF_WEIB"			/* (-ln(1-p) ^ (1/k)) * J */
			xIN MONADIC
			GSB qf_check_probability
			GSB log1m
			+/-
			RCL K
			GSB check_special_pos
			1/x
			y[^x]
			RCL J
			GSB check_special_pos
			[times]
			xOUT xOUT_NORMAL



/* This file is part of 34S.

 34S is free software: you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation, either version 3 of the License, or
 (at your option) any later version.


 34S is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.

 You should have received a copy of the GNU General Public License
 along with 34S.  If not, see <http://www.gnu.org/licenses/>.
*/

// ====================================================================
// XXX This preprocessor-ready example program requires that it be
//     assembled with the '-pp' switch.
//     See post-processed output in wp34s_pp.lst file.
// ====================================================================

// See http://www.hpmuseum.org/cgi-sys/cgiwrap/hpmuseum/articles.cgi?read=700 */
// This is the WP-34s version of the eight queens solver from that article:

001: _8Queens::   ****LBL'8Qu' // Entry point
002:              CLREGS
003:              8
004:              STO 11
005: _8Qu_Loop::  RCL 11
006:              x=? 00
007:              JMP _8Qu_Done // SKIP 22
008:              INC 00
009:              STO[->]00
010: _8Qu_Again:: INC 10
011:              RCL 00
012:              STO 09
013: _8Qu_Loop2:: DEC 09
014:              RCL 09
015:              x=0?
016:              JMP _8Qu_Loop // BACK 11
017:              RCL[->]00
018:              RCL-[->]09
019:              x=0?
020:              JMP _8Qu_Not0 // SKIP 05
021:              ABS
022:              RCL 00
023:              RCL- 09
024:              x[!=]? Y
025:              JMP _8Qu_Loop2 // BACK 12
026: _8Qu_Not0::  DSZ[->]00
027:              JMP _8Qu_Again // BACK 17
028:              DSZ 00
029:              JMP _8Qu_Not0 // BACK 03
030: _8Qu_Done::  RCL 10
031:              END

/*
 Checksum: 8024

 When run the output is 876 in X.
 Run time on the hardware is 2.3 - 2.4 seconds in float mode and 2.1 seconds in
 integer mode.

 This program runs in floating point mode or integer mode (base 9 or above).
*/
